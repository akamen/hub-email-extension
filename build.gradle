apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'application'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'
version = '1.0.0-SNAPSHOT'
description = ''
mainClassName = 'com.blackducksoftware.integration.email.Application'
applicationDefaultJvmArgs = ['-DAPP_HOME=APP_HOME_VAR_TOKEN', '-Dlog4j.configurationFile=APP_HOME_VAR_TOKEN/config/log4j.xml', '-Dext.config.location=APP_HOME_VAR_TOKEN/config']

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "https://maven.restlet.com" }
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile 'com.blackducksoftware.integration:hub-common:1.4.6-SNAPSHOT'
    
    // logging dependencies
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.6.2'
	compile 'org.apache.logging.log4j:log4j-core:2.6.2'
	runtime group: 'org.restlet.jse', name: 'org.restlet.ext.slf4j', version: '2.3.7'
	
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'commons-io:commons-io:2.5'
	compile 'commons-cli:commons-cli:1.3.1'
	compile 'com.google.code.gson:gson:2.7'
	compile 'org.jsoup:jsoup:1.9.2'
	compile 'javax.mail:mail:1.4.7'
	compile 'org.freemarker:freemarker:2.3.25-incubating'
	
	// hub-extension specific dependencies.
	compile 'javax.inject:javax.inject:1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
	compile 'com.google.guava:guava:19.0'
	compile 'org.springframework:spring-context:4.2.5.RELEASE'
	compile 'org.restlet.jse:org.restlet.ext.oauth:2.3.7'
	compile 'org.restlet.jse:org.restlet.ext.jackson:2.3.7'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.0'
	
	testCompile 'junit:junit:4.12'
}

jar { 
	sourceSets {
		main {
			resources {
				exclude 'customer.properties'
				exclude '**/images/*'
				exclude 'images'
				exclude '**/templates/*'
				exclude 'templates'
				exclude 'log4j.xml'
				exclude 'config-options.json'
			}
		}
	}
}

startScripts {
  doLast {
    unixScript.text = unixScript.text.replace("APP_HOME_VAR_TOKEN", "\$APP_HOME")
    windowsScript.text = windowsScript.text.replace("APP_HOME_VAR_TOKEN", "%APP_HOME%")
  }
}

distributions {
    main {
        contents {
            from ('src/main/resources/images/') {into 'images/'}
            from('src/main/resources/templates/') {into 'templates/'}
            from('src/main/resources/customer.properties') {into 'config/'}
            from('src/main/resources/log4j.xml') {into 'config/'}
            from('src/main/resources/config-options.json') {into 'config/'}
        }
    }
}
